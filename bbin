#!/usr/bin/env bb

; :bbin/start
;
; {:coords {:bbin/url "https://raw.githubusercontent.com/babashka/bbin/v0.0.9/bbin"}}
;
; :bbin/end

(babashka.deps/add-deps
  '{:deps {org.babashka/cli {:mvn/version "0.3.35"},
           babashka/process {:mvn/version "0.1.7"},
           fipp/fipp {:mvn/version "0.6.26"},
           com.taoensso/timbre {:mvn/version "5.2.1"},
           selmer/selmer {:mvn/version "1.12.53"},
           io.github.rads/deps-info {:git/tag "v0.0.1", :git/sha "1e4eb28"}}})

(ns babashka.bbin.util
  (:require [babashka.fs :as fs]
            [babashka.process :as p]
            [clojure.pprint :as pprint]
            [clojure.string :as str]
            [taoensso.timbre :as log])
  (:import (java.util Date)))

(defn sh [cmd & {:as opts}]
  (doto (p/sh cmd (merge {:err :inherit} opts))
    p/check))

(defn set-logging-config! [{:keys [debug]}]
  (log/merge-config! {:min-level (if debug :debug :warn)}))

(defn pprint [x _]
  (pprint/pprint x))

(defn print-help [& _]
  (println (str/trim "
Usage: bbin <command>

  bbin install    Install a script
  bbin uninstall  Remove a script
  bbin ls         List installed scripts
  bbin bin        Display bbin bin folder")))

(defn now []
  (Date.))

(def ^:dynamic *bbin-root* (fs/expand-home "~/.bbin"))

(defn bbin-root [_]
  *bbin-root*)

(defn bin-dir [cli-opts]
  (fs/file (bbin-root cli-opts) "bin"))

(defn canonicalized-cli-opts [cli-opts]
  (merge cli-opts
         (when-let [v (:local/root cli-opts)]
           {:local/root (str (fs/canonicalize v {:nofollow-links true}))})))

(defn ensure-bbin-dirs [cli-opts]
  (fs/create-dirs (bin-dir cli-opts)))

(def reserved-script-names
  #{"alias"
    "autoload"
    "bg"
    "bind"
    "bindkey"
    "break"
    "builtin"
    "bye"
    "case"
    "cd"
    "chdir"
    "command"
    "compadd"
    "comparguments"
    "compcall"
    "compctl"
    "compdescribe"
    "compfiles"
    "compgroups"
    "complete"
    "compquote"
    "compset"
    "comptags"
    "comptry"
    "compvalues"
    "continue"
    "declare"
    "dirs"
    "disable"
    "disown"
    "echo"
    "echotc"
    "echoti"
    "emulate"
    "enable"
    "eval"
    "exec"
    "exit"
    "export"
    "false"
    "fc"
    "fg"
    "float"
    "for"
    "function"
    "functions"
    "getln"
    "getopts"
    "hash"
    "help"
    "history"
    "integer"
    "jobs"
    "kill"
    "let"
    "limit"
    "local"
    "logout"
    "ls"
    "man"
    "noglob"
    "popd"
    "print"
    "printf"
    "private"
    "pushd"
    "pushln"
    "pwd"
    "r"
    "rm"
    "read"
    "readonly"
    "rehash"
    "return"
    "sched"
    "select"
    "set"
    "setopt"
    "shift"
    "source"
    "sudo"
    "su"
    "suspend"
    "test"
    "times"
    "trap"
    "true"
    "ttyctl"
    "type"
    "typeset"
    "ulimit"
    "umask"
    "unalias"
    "unfunction"
    "unhash"
    "unlimit"
    "unset"
    "unsetopt"
    "vared"
    "variables"
    "wait"
    "whence"
    "where"
    "which"
    "zcompile"
    "zformat"
    "zle"
    "zmodload"
    "zparseopts"
    "zregexparse"
    "zstyle"})

(ns babashka.bbin.scripts
  (:require [babashka.fs :as fs]
            [babashka.deps :as deps]
            [rads.deps-info.infer :as deps-info-infer]
            [rads.deps-info.summary :as deps-info-summary]
            [clojure.string :as str]
            [clojure.edn :as edn]
            [clojure.pprint :as pprint]
            [selmer.parser :as selmer]
            [selmer.util :as selmer-util]
            [babashka.bbin.util :as util :refer [sh]]))

(defn- bbin-lib-str? [x]
  #{"io.github.babashka/bbin"
    "com.github.babashka/bbin"}
  (str x))

(defn- bbin-git-url? [coords]
  (or (re-seq #"^https://github.com/babashka/bbin(\.git)?$" (:git/url coords))
      (= "git@github.com:babashka/bbin.git" (:git/url coords))))

(defn- bbin-http-url? [coords]
  (str/starts-with? (:bbin/url coords) "https://raw.githubusercontent.com/babashka/bbin/"))

(defn- valid-bbin-lib? [{:keys [lib coords] :as _header}]
  (or (and (bbin-lib-str? lib)
           (or (= #{:git/tag :git/sha} (set (keys coords)))
               (and (:git/url coords) (bbin-git-url? coords))))
      (and (= #{:bbin/url} (set (keys coords)))
           (bbin-http-url? coords))))

(defn- throw-invalid-bbin-script-name [script-name header]
  (throw (ex-info (str "Invalid script name.\nThe `bbin` name is reserved for "
                       "installing `bbin` from the official repo.\nUse `--as` "
                       "to choose a different name.")
                  (merge {:script/name script-name} header))))

(defn- throw-reserved-script-name [script-name]
  (let [msg (format
              (str "Invalid script name.\nThe name `%s` cannot be used because "
                   "it may conflict with a system command.\nUse `--as` "
                   "to choose a different name.")
              script-name)]
    (throw (ex-info msg {:script/name script-name}))))

(defn assert-valid-script-name [script-name header]
  (when (contains? util/reserved-script-names script-name)
    (throw-reserved-script-name script-name))
  (when (and (#{"bbin"} script-name) (not (valid-bbin-lib? header)))
    (throw-invalid-bbin-script-name script-name header)))

(defn- pprint [x _]
  (pprint/pprint x))

(defn- gitlib-path [cli-opts script-deps]
  (let [coords (val (first script-deps))]
    (fs/expand-home (str "~/.gitlibs/libs/" (:script/lib cli-opts) "/" (:git/sha coords)))))

(def ^:private tool-template-str
  "#!/usr/bin/env bash
set -e

# :bbin/start
#
{{script/meta}}
#
# :bbin/end

SCRIPT_ROOT='{{script/root}}'
SCRIPT_LIB='{{script/lib}}'
SCRIPT_COORDS='{{script/coords}}'
SCRIPT_NS_DEFAULT='{{script/ns-default}}'
SCRIPT_NAME=\"$(basename \"$0\")\"

if [ -z $1 ]; then
  echo \"Usage: $SCRIPT_NAME <command>\"
  echo
  bb \\
    --deps-root \"$SCRIPT_ROOT\" \\
    --config <(echo \"{:deps {$SCRIPT_LIB $SCRIPT_COORDS}}\") \\
    -e \"
      (require '$SCRIPT_NS_DEFAULT)
      (def fns (filter #(fn? (deref (val %))) (ns-publics '$SCRIPT_NS_DEFAULT)))
      (def max-width (->> (keys fns) (map (comp count str)) (apply max)))
      (defn pad-right [x] (format (str \\\"%-\\\" max-width \\\"s\\\") x))
      (doseq [[k v] fns]
        (println
          (str \\\"  $SCRIPT_NAME \\\" (pad-right k) \\\"  \\\"
               (when (:doc (meta v))
                 (first (str/split-lines (:doc (meta v))))))))\"
else
  exec bb \\
    --deps-root \"$SCRIPT_ROOT\" \\
    --config <(echo \"{:deps {$SCRIPT_LIB $SCRIPT_COORDS}}\") \\
    -x $SCRIPT_NS_DEFAULT/$1 \\
    -- \"${@:2}\"
fi")

(def ^:private git-or-local-template-str
  "#!/usr/bin/env bash
set -e

# :bbin/start
#
{{script/meta}}
#
# :bbin/end

SCRIPT_ROOT='{{script/root}}'
SCRIPT_LIB='{{script/lib}}'
SCRIPT_COORDS='{{script/coords}}'
SCRIPT_MAIN_OPTS_FIRST='{{script/main-opts.0}}'
SCRIPT_MAIN_OPTS_SECOND='{{script/main-opts.1}}'

exec bb \\
  --deps-root \"$SCRIPT_ROOT\" \\
  --config <(echo \"{:deps {$SCRIPT_LIB $SCRIPT_COORDS}}\") \\
  $SCRIPT_MAIN_OPTS_FIRST \"$SCRIPT_MAIN_OPTS_SECOND\" \\
  -- \"$@\"")

(defn- http-url->script-name [http-url]
  (first
    (str/split (last (str/split http-url #"/"))
               #"\.")))

(defn- bb-shebang? [s]
  (str/starts-with? s "#!/usr/bin/env bb"))

(defn insert-script-header [script-contents header]
  (let [
        prev-lines (str/split-lines script-contents)
        [prefix [shebang & code]] (split-with #(not (bb-shebang? %)) prev-lines)
        next-lines (concat prefix [shebang]
                           [""
                            "; :bbin/start"
                            ";"]
                           (map #(str "; " %)
                                (str/split-lines
                                  (with-out-str
                                    (pprint/pprint header))))
                           [";"
                            "; :bbin/end"]
                           code)]
    (str/join "\n" next-lines)))

(defn- install-http [cli-opts]
  (let [http-url (:script/lib cli-opts)
        script-deps {:bbin/url http-url}
        header {:coords script-deps}
        _ (pprint header cli-opts)
        script-name (or (:as cli-opts) (http-url->script-name http-url))
        _ (assert-valid-script-name script-name header)
        script-contents (-> (slurp (:bbin/url script-deps))
                            (insert-script-header header))
        script-file (fs/canonicalize (fs/file (util/bin-dir cli-opts) script-name)
                                     {:nofollow-links true})]
    (if (:dry-run cli-opts)
      (pprint {:script-file (str script-file)
               :script-contents script-contents}
              cli-opts)
      (do
        (spit (str script-file) script-contents)
        (sh ["chmod" "+x" (str script-file)])
        nil))))

(defn- default-script-config [cli-opts]
  (let [[ns name] (str/split (:script/lib cli-opts) #"/")
        top (last (str/split ns #"\."))]
    {:main-opts ["-m" (str top "." name)]
     :ns-default (str top "." name)}))

(defn- install-deps-git-or-local [cli-opts]
  (let [script-deps (deps-info-infer/infer (assoc cli-opts :lib (:script/lib cli-opts)))
        header {:lib (key (first script-deps))
                :coords (val (first script-deps))}
        _ (pprint header cli-opts)
        _ (deps/add-deps {:deps script-deps})
        script-root (fs/canonicalize (or (:local/root cli-opts) (gitlib-path cli-opts script-deps)) {:nofollow-links true})
        bb-file (fs/file script-root "bb.edn")
        bb-edn (when (fs/exists? bb-file)
                 (some-> bb-file slurp edn/read-string))
        script-name (or (:as cli-opts)
                        (some-> (:bbin/bin bb-edn) first key str)
                        (second (str/split (:script/lib cli-opts) #"/")))
        _ (assert-valid-script-name script-name header)
        script-config (merge (default-script-config cli-opts)
                             (some-> (:bbin/bin bb-edn) first val)
                             (when (:ns-default cli-opts)
                               {:ns-default (edn/read-string (:ns-default cli-opts))}))
        script-edn-out (with-out-str
                         (binding [*print-namespace-maps* false]
                           (clojure.pprint/pprint header)))
        tool-mode (or (:tool cli-opts)
                      (and (some-> (:bbin/bin bb-edn) first val :ns-default)
                           (not (some-> (:bbin/bin bb-edn) first val :main-opts))))
        main-opts (or (some-> (:main-opts cli-opts) edn/read-string)
                      (:main-opts script-config))
        template-opts {:script/meta (->> script-edn-out
                                         str/split-lines
                                         (map #(str "# " %))
                                         (str/join "\n"))
                       :script/root script-root
                       :script/lib (pr-str (key (first script-deps)))
                       :script/coords (binding [*print-namespace-maps* false] (pr-str (val (first script-deps))))}
        template-opts' (if tool-mode
                         (assoc template-opts :script/ns-default (:ns-default script-config))
                         (assoc template-opts :script/main-opts
                                              [(first main-opts)
                                               (if (= "-f" (first main-opts))
                                                 (fs/canonicalize (fs/file script-root (second main-opts))
                                                                  {:nofollow-links true})
                                                 (second main-opts))]))
        template-str (if tool-mode
                       tool-template-str
                       git-or-local-template-str)
        template-out (selmer-util/without-escaping
                       (selmer/render template-str template-opts'))
        script-file (fs/canonicalize (fs/file (util/bin-dir cli-opts) script-name) {:nofollow-links true})]
    (if (:dry-run cli-opts)
      (pprint {:script-file (str script-file)
               :template-out template-out}
              cli-opts)
      (do
        (spit (str script-file) template-out)
        (sh ["chmod" "+x" (str script-file)])
        nil))))

(def ^:private maven-template-str
  "#!/usr/bin/env bash
set -e

# :bbin/start
#
{{script/meta}}
#
# :bbin/end

SCRIPT_LIB='{{script/lib}}'
SCRIPT_COORDS='{{script/coords}}'
SCRIPT_MAIN_OPTS_FIRST='{{script/main-opts.0}}'
SCRIPT_MAIN_OPTS_SECOND='{{script/main-opts.1}}'

exec bb \\
  --config <(echo \"{:deps {$SCRIPT_LIB $SCRIPT_COORDS}}\") \\
  $SCRIPT_MAIN_OPTS_FIRST \"$SCRIPT_MAIN_OPTS_SECOND\" \\
  -- \"$@\"")

(defn- install-deps-maven [cli-opts]
  (let [script-deps {(edn/read-string (:script/lib cli-opts))
                     (select-keys cli-opts [:mvn/version])}
        header {:lib (key (first script-deps))
                :coords (val (first script-deps))}
        _ (pprint header cli-opts)
        _ (deps/add-deps {:deps script-deps})
        script-root (fs/canonicalize (or (:local/root cli-opts) (gitlib-path cli-opts script-deps)) {:nofollow-links true})
        script-name (or (:as cli-opts) (second (str/split (:script/lib cli-opts) #"/")))
        _ (assert-valid-script-name script-name header)
        script-config (default-script-config cli-opts)
        script-edn-out (with-out-str
                         (binding [*print-namespace-maps* false]
                           (clojure.pprint/pprint header)))
        main-opts (or (some-> (:main-opts cli-opts) edn/read-string)
                      (:main-opts script-config))
        template-opts {:script/meta (->> script-edn-out
                                         str/split-lines
                                         (map #(str "# " %))
                                         (str/join "\n"))
                       :script/root script-root
                       :script/lib (pr-str (key (first script-deps)))
                       :script/coords (binding [*print-namespace-maps* false] (pr-str (val (first script-deps))))
                       :script/main-opts [(first main-opts)
                                          (if (= "-f" (first main-opts))
                                            (fs/canonicalize (fs/file script-root (second main-opts))
                                                             {:nofollow-links true})
                                            (second main-opts))]}
        template-out (selmer-util/without-escaping
                       (selmer/render maven-template-str template-opts))
        script-file (fs/canonicalize (fs/file (util/bin-dir cli-opts) script-name) {:nofollow-links true})]
    (if (:dry-run cli-opts)
      (pprint {:script-file (str script-file)
               :template-out template-out}
              cli-opts)
      (do
        (spit (str script-file) template-out)
        (sh ["chmod" "+x" (str script-file)])
        nil))))

(defn- parse-script [s]
  (let [lines (str/split-lines s)
        prefix (if (str/ends-with? (first lines) "bb") ";" "#")]
    (->> lines
         (drop-while #(not (re-seq (re-pattern (str "^" prefix " *:bbin/start")) %)))
         next
         (take-while #(not (re-seq (re-pattern (str "^" prefix " *:bbin/end")) %)))
         (map #(str/replace % (re-pattern (str "^" prefix " *")) ""))
         (str/join "\n")
         edn/read-string)))

(defn load-scripts [cli-opts]
  (->> (file-seq (util/bin-dir cli-opts))
       (filter #(.isFile %))
       (map (fn [x] [(symbol (str (fs/relativize (util/bin-dir cli-opts) x)))
                     (parse-script (slurp x))]))
       (into {})))

(defn ls [cli-opts]
  (-> (load-scripts cli-opts)
      (util/pprint cli-opts)))

(defn bin [cli-opts]
  (println (str (util/bin-dir cli-opts))))

(defn install [cli-opts]
  (if-not (:script/lib cli-opts)
    (util/print-help)
    (do
      (util/ensure-bbin-dirs cli-opts)
      (let [cli-opts' (util/canonicalized-cli-opts cli-opts)
            {:keys [procurer]} (deps-info-summary/summary cli-opts')]
        (case procurer
          :http (install-http cli-opts')
          :maven (install-deps-maven cli-opts')
          :git (install-deps-git-or-local cli-opts')
          :local (install-deps-git-or-local cli-opts'))))))

(defn uninstall [cli-opts]
  (if-not (:script/lib cli-opts)
    (util/print-help)
    (do
      (util/ensure-bbin-dirs cli-opts)
      (let [script-name (:script/lib cli-opts)
            script-file (fs/canonicalize (fs/file (util/bin-dir cli-opts) script-name) {:nofollow-links true})]
        (when (fs/delete-if-exists script-file)
          (println "Removing" (str script-file)))))))

(ns babashka.bbin.cli
  (:require [babashka.cli :as cli]
            [babashka.bbin.scripts :as scripts]
            [babashka.bbin.util :as util]
            [clojure.string :as str]))

(declare print-commands)

(defn- commands
  [& {:keys [install-fn uninstall-fn ls-fn bin-fn]}]
  [{:cmds ["commands"]
    :fn #(print-commands %)}

   {:cmds ["help"]
    :fn util/print-help}

   {:cmds ["install"]
    :fn #(install-fn (:opts %))
    :args->opts [:script/lib]
    :aliases {:T :tool}}

   {:cmds ["uninstall"]
    :fn #(uninstall-fn (:opts %))
    :args->opts [:script/lib]}

   {:cmds ["ls"]
    :fn #(ls-fn (:opts %))}

   {:cmds ["bin"]
    :fn #(bin-fn (:opts %))}

   {:cmds []
    :fn util/print-help
    :aliases {:h :help}}])

(defn- print-commands [_]
  (println (str/join " " (keep #(first (:cmds %)) (commands nil)))))

(def default-run-opts
  {:install-fn scripts/install
   :uninstall-fn scripts/uninstall
   :ls-fn scripts/ls
   :bin-fn scripts/bin})

(defn bbin [main-args & {:as run-opts}]
  (let [run-opts' (merge default-run-opts run-opts)]
    (util/set-logging-config! (cli/parse-opts main-args))
    (cli/dispatch (commands run-opts') main-args {})))

(defn -main [& args]
  (bbin args))

(when (= *file* (System/getProperty "babashka.file"))
  (apply -main *command-line-args*))
